// Created from template at https://docs.gradle.org/current/userguide/publishing_maven.html#publishing_maven:complete_example

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'com.lacunasoftware.restpki'
archivesBaseName = 'restpkicore-client'
version = '1.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.1'
    implementation 'io.swagger.core.v3:swagger-annotations:2.0.0'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.lacunasoftware.restpki'
            artifactId = 'restpkicore-client'
            from components.java
            pom {
                name = 'Rest PKI Core client library for Java 8+'
                description = 'Classes to consume Lacuna Software REST PKI Core'
                url = 'https://github.com/LacunaSoftware/RestPkiNGJavaClient'
                licenses {
                    license {
                        name = 'Unlicense'
                    }
                }
                developers {
                    developer {
                        id = 'LeonardoP'
                        name = 'Leonardo Pignataro'
                        email = 'leonardop@lacunasoftware.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:LacunaSoftware/RestPkiNGJavaClient.git'
                    developerConnection = 'scm:git:git@github.com:LacunaSoftware/RestPkiNGJavaClient.git'
                    url = 'https://github.com/LacunaSoftware/RestPkiNGJavaClient'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
